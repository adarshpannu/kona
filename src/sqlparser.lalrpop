use std::str::FromStr;
use lalrpop_util::ParseError;

use crate::includes::*;

use crate::ast::{Expr::*, *};
use crate::row::*;
use crate::graph::Graph;
use std::rc::Rc;
use std::cell::RefCell;
use crate::ast::{AST, NamedExpr, QueryBlock, QueryBlockType, QGM, ParserState, Quantifier, Ordering};

grammar(parser_state: &mut ParserState);

match {
    r"(?i)CATALOG" => "CATALOG",
    r"(?i)DESCRIBE" => "DESCRIBE",
    r"(?i)TABLE" => "TABLE",
    r"(?i)SELECT" => "SELECT",
    r"(?i)FROM" => "FROM",
    r"(?i)WHERE" => "WHERE",
    r"(?i)GROUP" => "GROUP",
    r"(?i)ORDER" => "ORDER",
    r"(?i)HAVING" => "HAVING",
    r"(?i)ASC" => "ASC",
    r"(?i)DESC" => "DESC",
    r"(?i)BY" => "BY",
    r"(?i)AND" => "AND",
    r"(?i)OR" => "OR",
    r"(?i)AS" => "AS",
    r"(?i)WITH" => "WITH",
    r"(?i)NOT" => "NOT",
    r"(?i)EXISTS" => "EXISTS",
    r"(?i)DISTINCT" => "DISTINCT",
    r"(?i)IN" => "IN",
    r"(?i)ANY" => "ANY",
    r"(?i)ALL" => "ALL",
    r"(?i)LIKE" => "LIKE",
    r"(?i)BETWEEN" => "BETWEEN",
    r"(?i)MIN" => "MIN",
    r"(?i)MAX" => "MAX",
    r"(?i)SUM" => "SUM",
    r"(?i)COUNT" => "COUNT",
    r"(?i)AVG" => "AVG",
    r"(?i)CAST" => "CAST",
    r"(?i)TOP" => "TOP",
    r"(?i)IS" => "IS",
    r"(?i)NULL" => "NULL",
    "(",
    ")",
    "=",
    "<",
    "<=",
    ">",
    ">=",
    "!=",
    "<>",
    "+",
    "-",
    "*",
    "/",
    r"[-+]?[0-9]+",
    r"[-+]?\d*\.\d*",
    r#""[^"]*""#,
    r#"'[^']*'"#,
    ".",
    ",",
    ";",
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip // comments
    r"--[^\n\r]*[\n\r]*" => { }, // Skip -- comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
} else {
    r"[a-zA-Z][a-zA-Z0-9_-]*",
}

CommaSeparatedList<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

SemicolonSeparatedList<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

// -------------------- STATEMENTS --------------------
pub Job = SemicolonSeparatedList<Statement>;

pub Statement: AST = {
    <CatalogTable>,
    <DescribeTable>,
    <FullSelect>
}

// -------------------- CATALOG TABLE --------------------
pub CatalogTable: AST = {
    "CATALOG" "TABLE" <name:Identifier> "(" <options:CatalogTableOptions> ")"  => AST::CatalogTable { name: name, options: options }
}

CatalogTableOptions = CommaSeparatedList<CatalogTableOption>;

CatalogTableOption: (String, String) = {
    <opt:QuotedString> "=" <val:QuotedString> => (opt, val)
}

// -------------------- DESCRIBE TABLE --------------------
pub DescribeTable: AST = {
    "DESCRIBE" "TABLE" <name:Identifier>  => AST::DescribeTable { name: name }
}

// -------------------- QUERY --------------------

pub FullSelect: AST = <cte_list:WithClause?> <mut qblock:QueryBlock> => {
    qblock.name = Some(String::from("main"));
    qblock.qbtype = QueryBlockType::Main;

    let graph = std::mem::replace(&mut parser_state.graph, Graph::new());

    let qgm = QGM { qblock, cte_list: cte_list.unwrap_or(vec![]), graph };
    AST::QGM(qgm)
};

pub QueryBlock: QueryBlock = {
    "SELECT" TopN? <distinct:AllOrDistinct?> 
    <select_list: SelectList> 
    "FROM" <mut quns: FromList> 
    <pred_list: WhereClause?> 
    <group_by:GroupByClause?> 
    <having:HavingClause?> 
    <order_by:OrderByClause?>  => {
        //let pred_list = if pred_list.is_some() { Some(pred_list.unwrap()) } else { None };
        QueryBlock::new(parser_state.next_id(), None, QueryBlockType::Subquery, 
            select_list, quns, pred_list, group_by, having, order_by, distinct.unwrap_or(DistinctProperty::All), None)
    }
};

Subquery: QueryBlockLink = "(" <qblock:QueryBlock> ")" => {
    let subq = Rc::new(RefCell::new(qblock));
    subq
};

TopN = "TOP" <Integer>;

AllOrDistinct: DistinctProperty = {
    "ALL" => DistinctProperty::All,
    "DISTINCT" => DistinctProperty::Distinct
}

WithClause = "WITH" <CTEList>;

CTEList = CommaSeparatedList<CTE>;

CTE: QueryBlockLink = <name:Identifier> "AS" <mut subqlink:Subquery> => {
    let mut subq = &mut *subqlink.borrow_mut();
    subq.name = Some(name);
    subq.qbtype = QueryBlockType::CTE;
    subqlink.clone()
};

SelectList = CommaSeparatedList<NamedExpr>;

FromList = CommaSeparatedList<Quantifier>;

WhereClause = "WHERE" <LogExpr>;

GroupByClause = "GROUP" "BY" <CommaSeparatedList<Expr>>;

HavingClause = "HAVING" <LogExpr>;

OrderByClause = "ORDER" "BY" <CommaSeparatedList<OrderCol>>;

OrderCol: (ExprLink, Ordering) = <e:Expr> <o:OrderSort?> => (e, o.unwrap_or(Ordering::Asc));

OrderSort: Ordering = {
    "ASC" => Ordering::Asc,
    "DESC" => Ordering::Desc
}

Quantifier: Quantifier = {
    <schemaname:(Identifier ".")?> <name:Identifier> <alias:correlation_clause?> => {
        Quantifier::new(parser_state.next_id(), Some(name), alias, None)
    },
    <subq:Subquery> <alias:correlation_clause?> => {
        Quantifier::new(parser_state.next_id(), None, alias, Some(subq))
    }
}

correlation_clause: String = "AS"? <id:Identifier> => id;

NamedExpr: NamedExpr = {
    "*" => NamedExpr::new(None, parser_state.graph.add_node(Star, None)),
    <expr:Expr> <name:("AS" Identifier)?> => NamedExpr::new(name.map(|e| e.1), expr)
}

pub LogExpr: ExprLink = {
    <LogExprFactor>,
    <l:LogExpr> "OR" <r:LogExprFactor> => parser_state.graph.add_node(LogExpr(LogOp::Or), Some(vec![l, r]))
}

LogExprFactor: ExprLink = {
    <l:LogExprFactor> "AND" <r:RelExpr> => parser_state.graph.add_node(LogExpr(LogOp::And), Some(vec![l, r])),
    RelExpr,
}

RelExpr: ExprLink = {
    <l:Expr> <op:RelOp> <r:RelExprRHS> => parser_state.graph.add_node(RelExpr(op), Some(vec![l, r])),
    <l:Expr> "IS" <not:"NOT"?> "NULL" => {
        let r = parser_state.graph.add_node(Literal(Datum::NULL), None);
        let expr = parser_state.graph.add_node(RelExpr(RelOp::Is), Some(vec![l, r]));
        if not.is_some() {
            parser_state.graph.add_node(LogExpr(LogOp::Not), Some(vec![expr]))
        } else {
            expr
        }
    },
    <l:Expr> <not:"NOT"?> "LIKE" <r:RelExprRHS> => {
        let expr = parser_state.graph.add_node(RelExpr(RelOp::Like), Some(vec!(l, r)));
        if not.is_some() {
            parser_state.graph.add_node(LogExpr(LogOp::Not), Some(vec![expr]))
        } else {
            expr
        }
    },
    <e:Expr> "BETWEEN" <l:Expr> "AND" <r:Expr> => parser_state.graph.add_node(BetweenExpr, Some(vec![e, l, r])),
    <e:Expr> <not:"NOT"?> "IN" <mut inexpr:InExprRHS> => {
        let expr = if inexpr.0 {
            let subq = inexpr.1.pop().unwrap();
            parser_state.graph.add_node(InSubqExpr, Some(vec![e, subq]))
        } else {
            let mut args = inexpr.1;
            let mut children = vec![e];
            children.append(&mut args);
            parser_state.graph.add_node(InListExpr, Some(children))
        };
        if not.is_some() {
            parser_state.graph.add_node(LogExpr(LogOp::Not), Some(vec![expr]))
        } else {
            expr
        }
    },
    "EXISTS" <subq:Subquery> => { 
        let subq = parser_state.graph.add_node(Subquery(subq), None);
        parser_state.graph.add_node(ExistsExpr, Some(vec![subq]))
    },
    "NOT" <expr:RelExpr> => parser_state.graph.add_node(LogExpr(LogOp::Not), Some(vec![expr])),
    "(" <LogExpr> ")",
}

RelExprRHS: ExprLink = {
    <rhs:Expr> => rhs,
    <prefix:SubqueryQuantifier> <subq:Subquery> => parser_state.graph.add_node(Subquery(subq), None)
}

RelOp: RelOp = {
    ">" => RelOp::Gt,
    ">=" => RelOp::Ge,
    "<" => RelOp::Lt,
    "<=" => RelOp::Le,
    "!=" => RelOp::Ne,
    "<>" => RelOp::Ne,
    "=" => RelOp::Eq,
}

InExprRHS: (bool, Vec<ExprLink>) = {
    <subq:Subquery> => (true, vec![parser_state.graph.add_node(Subquery(subq), None)]),
    "(" <InList> ")" => (false, <>)
}

InList = CommaSeparatedList<Expr>;

SubqueryQuantifier = {
    "ALL",
    "ANY",
}

pub Expr: ExprLink = {
    <l:Expr> "+" <r:Factor> => parser_state.graph.add_node(BinaryExpr(ArithOp::Add), Some(vec!(l, r))),
    <l:Expr> "-" <r:Factor> => parser_state.graph.add_node(BinaryExpr(ArithOp::Sub), Some(vec!(l, r))),
    Factor,
}

Factor: ExprLink = {
    <l:Factor> "*" <r:Term> => parser_state.graph.add_node(BinaryExpr(ArithOp::Mul), Some(vec!(l, r))),
    <l:Factor> "/" <r:Term> => parser_state.graph.add_node(BinaryExpr(ArithOp::Div), Some(vec!(l, r))),
    Term,
}

Term: ExprLink = {
    <cr:ColumnReference> => parser_state.graph.add_node(Column { tablename: cr.0, colname: cr.1 }, None),
    QuotedString => parser_state.graph.add_node(Literal(Datum::STR(Box::new(<>))), None),
    Number,
    ScalarFunction,
    AggFunction,
    "-" <Term> => parser_state.graph.add_node(NegatedExpr, Some(vec![<>])),
    "(" <Expr> ")",
    <subq:Subquery> => parser_state.graph.add_node(Subquery(subq), None)
}

AggFunction: ExprLink = {
    <aggtype:AggName> "(" <arg:AggArg> ")" => parser_state.graph.add_node(AggFunction(aggtype), Some(vec![arg])),
    "COUNT" "(" <distinct:"DISTINCT"?> <arg:AggArg> ")" => {
        let aggtype = if distinct.is_some() { AggType::COUNT_DISTINCT } else { AggType::COUNT };
        parser_state.graph.add_node(AggFunction(aggtype), Some(vec![arg]))
    }
}

AggName: AggType = {
    "MIN" => AggType::MIN,
    "MAX" => AggType::MAX, 
    "SUM" => AggType::SUM, 
    "AVG" => AggType::AVG
}

AggArg: ExprLink = {
    <arg:Expr> => arg,
    "*" => parser_state.graph.add_node(Star, None)
}

ScalarFunction: ExprLink = {
    <name:Identifier> "(" <args:FunctionArgs> ")" => parser_state.graph.add_node(ScalarFunction(name), Some(args)),
    "CAST" "(" <arg:Expr> "AS" <datatype:Identifier> ")" => {
        let datatype = parser_state.graph.add_node(Literal(Datum::STR(Box::new(datatype.to_string()))), None);
        let args = vec![arg, datatype];
        parser_state.graph.add_node(ScalarFunction(String::from("CAST")), Some(args))
    }
}

FunctionArgs = CommaSeparatedList<Expr>;

Number: ExprLink = {
    Integer =>? { isize::from_str(<>)
        .map(|i| parser_state.graph.add_node(Literal(Datum::INT(i)), None))
        .map_err(|_| ParseError::User { error: "Invalid integer" }) 
    },
    Double =>? { f64::from_str(<>)
        .map(|d| parser_state.graph.add_node(Literal(Datum::DOUBLE(111, 222)), None))
        .map_err(|_| ParseError::User { error: "Invalid double" }) 
    },
}

Integer = r"[-+]?[0-9]+";

Double = r"[-+]?\d*\.\d*";

Identifier: String = {
    r"[a-zA-Z][a-zA-Z0-9_-]*" => <>.to_string()
}

ColumnReference: (Option<String>, String) = <tablename:(Identifier ".")?> <colname:Identifier> => (tablename.map(|e| e.0), colname);

QuotedString: String = {
    r#""[^"]*""# => {
        let s = <>;
        s[1..s.len()-1].to_string()
    },
    r#"'[^']*'"# => {
        let s = <>;
        s[1..s.len()-1].to_string()
    }
}
