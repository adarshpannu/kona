use std::str::FromStr;
use lalrpop_util::ParseError;

use crate::includes::*;

use crate::expr::{Expr::*, *};
use crate::row::*;
use crate::ast::*;
use crate::qgm::*;
use crate::graph::*;
use std::rc::Rc;

grammar(parser_state: &mut ParserState);

match {
    r"(?i)CATALOG" => "CATALOG",
    r"(?i)DESCRIBE" => "DESCRIBE",
    r"(?i)TABLE" => "TABLE",
    r"(?i)SELECT" => "SELECT",
    r"(?i)FROM" => "FROM",
    r"(?i)WHERE" => "WHERE",
    r"(?i)GROUP" => "GROUP",
    r"(?i)ORDER" => "ORDER",
    r"(?i)HAVING" => "HAVING",
    r"(?i)ASC" => "ASC",
    r"(?i)DESC" => "DESC",
    r"(?i)BY" => "BY",
    r"(?i)AND" => "AND",
    r"(?i)OR" => "OR",
    r"(?i)AS" => "AS",
    r"(?i)WITH" => "WITH",
    r"(?i)NOT" => "NOT",
    r"(?i)EXISTS" => "EXISTS",
    r"(?i)DISTINCT" => "DISTINCT",
    r"(?i)IN" => "IN",
    r"(?i)ANY" => "ANY",
    r"(?i)ALL" => "ALL",
    r"(?i)LIKE" => "LIKE",
    r"(?i)BETWEEN" => "BETWEEN",
    r"(?i)MIN" => "MIN",
    r"(?i)MAX" => "MAX",
    r"(?i)SUM" => "SUM",
    r"(?i)COUNT" => "COUNT",
    r"(?i)AVG" => "AVG",
    r"(?i)CAST" => "CAST",
    r"(?i)TOP" => "TOP",
    r"(?i)IS" => "IS",
    r"(?i)NULL" => "NULL",
    r"(?i)SET" => "SET",
    r"(?i)UNION" => "UNION",
    r"(?i)INTERSECT" => "INTERSECT",
    r"(?i)EXCEPT" => "EXCEPT",
    "(",
    ")",
    "=",
    "<",
    "<=",
    ">",
    ">=",
    "!=",
    "<>",
    "+",
    "-",
    "*",
    "/",
    r"[-+]?[0-9]+",
    r"[-+]?\d*\.\d*",
    r#""[^"]*""#,
    r#"'[^']*'"#,
    ".",
    ",",
    ";",
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip // comments
    r"--[^\n\r]*[\n\r]*" => { }, // Skip -- comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    r"(?i)LEFT" => "LEFT",
    r"(?i)RIGHT" => "RIGHT",
    r"(?i)FULL" => "FULL",
    r"(?i)INNER" => "INNER",
    r"(?i)OUTER" => "OUTER",
    r"(?i)JOIN" => "JOIN",
    r"(?i)ON" => "ON",
} else {
    r"[a-zA-Z][a-zA-Z0-9_-]*",
}

CommaSeparatedList<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T> => {
        v.push(e);
        v
    }
};

SemicolonSeparatedList<T>: Vec<T> = {
    <mut v:(<T> ";")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

// -------------------- STATEMENTS --------------------
pub Job = SemicolonSeparatedList<Statement>;

pub Statement: AST = {
    <CatalogTable>,
    <DescribeTable>,
    <SetOption>,
    <Query>
}

// -------------------- CATALOG TABLE --------------------
pub CatalogTable: AST = {
    "CATALOG" "TABLE" <name:Identifier> "(" <options:CatalogTableOptions> ")"  => AST::CatalogTable { name, options }
}

CatalogTableOptions = CommaSeparatedList<CatalogTableOption>;

CatalogTableOption: (String, Datum) = {
    <opt:IdentifierOrQuotedString> "=" <val:CatalogTableOptionValue> => (opt, val)
}

CatalogTableOptionValue: Datum = {
    Integer =>? { isize::from_str(<>)
        .map(|i| Datum::INT(i))
        .map_err(|_| ParseError::User { error: "Invalid integer" }) 
    },
    <str:IdentifierOrQuotedString> =>? {
        let str = Rc::new(str);
        Ok(Datum::STR(str))
    }
}


// -------------------- DESCRIBE TABLE --------------------
pub DescribeTable: AST = {
    "DESCRIBE" "TABLE" <name:Identifier>  => AST::DescribeTable { name: name }
}

// -------------------- SET OPTION --------------------
pub SetOption: AST = {
    "SET" <name:Identifier> "=" <value:CatalogTableOptionValue> => AST::SetOption { name, value }
}

// -------------------- U/I/E QUERY --------------------
pub UIEOperator: QueryBlockType = {
    "UNION" <all:"ALL"?> => if all.is_some() { QueryBlockType::UnionAll } else { QueryBlockType::Union },
    "INTERSECT" => QueryBlockType::Intersect,
    "EXCEPT" => QueryBlockType::Except,
}

// -------------------- FULL QUERY --------------------

pub Query: AST = <cte_list:WithClause?> <main_qblock_key:SUIEQuery> => {
    //main_qblock_key.name = Some(String::from("main"));
    //main_qblock_key.qbtype = QueryBlockType::Select;

    let expr_graph = replace(&mut parser_state.expr_graph, Graph::new());
    let qblock_graph = replace(&mut parser_state.qblock_graph, Graph::new());

    let qgm = QGM::new(main_qblock_key, cte_list.unwrap_or(vec![]), qblock_graph, expr_graph);
    AST::QGM(qgm)
};

// -------------------- SUIE (SELECT/UNION/INTERSECT/EXCEPT) QUERY  --------------------
pub SUIEQuery: QueryBlockKey = {
    QueryBlock,
    Subquery,
    <qb1:SUIEQuery> <qblocktype:UIEOperator> <qb2:QueryBlock> => {
        let qun1 = Quantifier::new_qblock(parser_state.expr_graph.next_id(), qb1, None);
        let qun2 = Quantifier::new_qblock(parser_state.expr_graph.next_id(), qb2, None);
        let qblock = QueryBlock::new(
            parser_state.qblock_graph.next_id(), 
            None, 
            qblocktype, 
            vec![], 
            vec![qun1, qun2], 
            None,
            None, 
            None, 
            None, 
            DistinctProperty::All,
            None);
        parser_state.qblock_graph.add_node(qblock, None)
    }
}

pub QueryBlock: QueryBlockKey = {
    "SELECT" TopN? <distinct:AllOrDistinct?> 
    <select_list: SelectList> 
    "FROM" <quns: FromList> 
    <pred_list: WhereClause?> 
    <group_by:GroupByClause?> 
    <having_clause:HavingClause?> 
    <order_by:OrderByClause?> => {
        let pred_list = pred_list.map(|pred| vec![pred]);
        let having_clause = having_clause.map(|pred| vec![pred]);
        let qblock = QueryBlock::new(parser_state.qblock_graph.next_id(), 
            None, 
            QueryBlockType::Select, 
            select_list, 
            quns, 
            pred_list,
            group_by, 
            having_clause, 
            order_by, 
            distinct.unwrap_or(DistinctProperty::All), 
            None);
        parser_state.qblock_graph.add_node(qblock, None)
    }
};

Subquery: QueryBlockKey = "(" <qblock:QueryBlock> ")" => {
    qblock
};

TopN = "TOP" <Integer>;

AllOrDistinct: DistinctProperty = {
    "ALL" => DistinctProperty::All,
    "DISTINCT" => DistinctProperty::Distinct
}

WithClause = "WITH" <CTEList>;

CTEList = CommaSeparatedList<CTE>;

CTE: QueryBlockKey = <name:Identifier> "AS" <subqkey:Subquery> => {
    let subq = &mut parser_state.qblock_graph.get_mut(subqkey).value;
    subq.name = Some(name);
    subq.qbtype = QueryBlockType::Select;
    subqkey
};

SelectList = CommaSeparatedList<NamedExpr>;

FromList = CommaSeparatedList<Quantifier>;

WhereClause = "WHERE" <LogExpr>;

GroupByClause = "GROUP" "BY" <CommaSeparatedList<Expr>>;

HavingClause = "HAVING" <LogExpr>;

OrderByClause = "ORDER" "BY" <CommaSeparatedList<OrderCol>>;

OrderCol: (ExprKey, Ordering) = <e:Expr> <o:OrderSort?> => (e, o.unwrap_or(Ordering::Asc));

OrderSort: Ordering = {
    "ASC" => Ordering::Asc,
    "DESC" => Ordering::Desc
}

Quantifier: Quantifier = {
    <schemaname:(Identifier ".")?> <name:Identifier> <alias:correlation_clause?> => {
        Quantifier::new_base(parser_state.expr_graph.next_id(), name, alias)
    },
    <subq:Subquery> <alias:correlation_clause> => {
        Quantifier::new_qblock(parser_state.expr_graph.next_id(), subq, Some(alias))
    },
    <ansi_join:AnsiJoin> => {
        Quantifier::new_ansijoin(parser_state.expr_graph.next_id(), ansi_join, None)
    }
}

AnsiJoin: AnsiJoin = { 
    <q1:Quantifier> <join_type:JoinType> <q2:Quantifier> "ON" <on_clause:LogExpr> => {
        let left = Box::new(q1);
        let right = Box::new(q2);
        AnsiJoin { join_type, left, right }
    },
    "(" <AnsiJoin> ")" 
}

JoinType: JoinType = {
    "INNER"? "JOIN" => JoinType::Inner,
    "LEFT" "OUTER"? "JOIN" => JoinType::Left,
    "RIGHT" "OUTER"? "JOIN" => JoinType::Right,
    "FULL" "OUTER"? "JOIN" => JoinType::Full,
}

correlation_clause: String = "AS"? <id:Identifier> => id;

NamedExpr: NamedExpr = {
    "*" => {
        let star = parser_state.expr_graph.add_node(Star, None);
        NamedExpr::new(None, star, &parser_state.expr_graph)
    },
    <expr:Expr> <alias:("AS" Identifier)?> => NamedExpr::new(alias.map(|e| e.1), expr, &parser_state.expr_graph)
}

pub LogExpr: ExprKey = {
    <LogExprFactor>,
    <l:LogExpr> "OR" <r:LogExprFactor> => parser_state.expr_graph.add_node(LogExpr(LogOp::Or), Some(vec![l, r]))
}

LogExprFactor: ExprKey = {
    <l:LogExprFactor> "AND" <r:RelExpr> => parser_state.expr_graph.add_node(LogExpr(LogOp::And), Some(vec![l, r])),
    RelExpr,
}

RelExpr: ExprKey = {
    <l:Expr> <op:RelOp> <r:RelExprRHS> => parser_state.expr_graph.add_node(RelExpr(op), Some(vec![l, r])),
    <l:Expr> "IS" <not:"NOT"?> "NULL" => {
        let r = parser_state.expr_graph.add_node(Literal(Datum::NULL), None);
        let expr = parser_state.expr_graph.add_node(RelExpr(RelOp::Is), Some(vec![l, r]));
        if not.is_some() {
            parser_state.expr_graph.add_node(LogExpr(LogOp::Not), Some(vec![expr]))
        } else {
            expr
        }
    },
    <l:Expr> <not:"NOT"?> "LIKE" <r:RelExprRHS> => {
        let expr = parser_state.expr_graph.add_node(RelExpr(RelOp::Like), Some(vec!(l, r)));
        if not.is_some() {
            parser_state.expr_graph.add_node(LogExpr(LogOp::Not), Some(vec![expr]))
        } else {
            expr
        }
    },
    <e:Expr> "BETWEEN" <l:Expr> "AND" <r:Expr> => parser_state.expr_graph.add_node(BetweenExpr, Some(vec![e, l, r])),
    <e:Expr> <not:"NOT"?> "IN" <mut inexpr:InExprRHS> => {
        let expr = if inexpr.0 {
            let subq = inexpr.1.pop().unwrap();
            parser_state.expr_graph.add_node(InSubqExpr, Some(vec![e, subq]))
        } else {
            let mut args = inexpr.1;
            let mut children = vec![e];
            children.append(&mut args);
            parser_state.expr_graph.add_node(InListExpr, Some(children))
        };
        if not.is_some() {
            parser_state.expr_graph.add_node(LogExpr(LogOp::Not), Some(vec![expr]))
        } else {
            expr
        }
    },
    "EXISTS" <subq:Subquery> => { 
        let subq = parser_state.expr_graph.add_node(Subquery(subq), None);
        parser_state.expr_graph.add_node(ExistsExpr, Some(vec![subq]))
    },
    "NOT" <expr:RelExpr> => parser_state.expr_graph.add_node(LogExpr(LogOp::Not), Some(vec![expr])),
    "(" <LogExpr> ")",
}

RelExprRHS: ExprKey = {
    <rhs:Expr> => rhs,
    <prefix:SubqueryQuantifier> <subq:Subquery> => parser_state.expr_graph.add_node(Subquery(subq), None)
}

RelOp: RelOp = {
    ">" => RelOp::Gt,
    ">=" => RelOp::Ge,
    "<" => RelOp::Lt,
    "<=" => RelOp::Le,
    "!=" => RelOp::Ne,
    "<>" => RelOp::Ne,
    "=" => RelOp::Eq,
}

InExprRHS: (bool, Vec<ExprKey>) = {
    <subq:Subquery> => (true, vec![parser_state.expr_graph.add_node(Subquery(subq), None)]),
    "(" <InList> ")" => (false, <>)
}

InList = CommaSeparatedList<Expr>;

SubqueryQuantifier = {
    "ALL",
    "ANY",
}

pub Expr: ExprKey = {
    <l:Expr> "+" <r:Factor> => parser_state.expr_graph.add_node(BinaryExpr(ArithOp::Add), Some(vec!(l, r))),
    <l:Expr> "-" <r:Factor> => parser_state.expr_graph.add_node(BinaryExpr(ArithOp::Sub), Some(vec!(l, r))),
    Factor,
}

Factor: ExprKey = {
    <l:Factor> "*" <r:Term> => parser_state.expr_graph.add_node(BinaryExpr(ArithOp::Mul), Some(vec!(l, r))),
    <l:Factor> "/" <r:Term> => parser_state.expr_graph.add_node(BinaryExpr(ArithOp::Div), Some(vec!(l, r))),
    Term,
}

Term: ExprKey = {
    <cr:ColumnReference> => parser_state.expr_graph.add_node(Column { prefix: cr.0, colname: cr.1, qunid: 0, colid: 0 }, None),
    QuotedString => parser_state.expr_graph.add_node(Literal(Datum::STR(Rc::new(<>))), None),
    Number,
    ScalarFunction,
    AggFunction,
    "-" <Term> => parser_state.expr_graph.add_node(NegatedExpr, Some(vec![<>])),
    "(" <Expr> ")",
    <subq:Subquery> => parser_state.expr_graph.add_node(Subquery(subq), None)
}

AggFunction: ExprKey = {
    <aggtype:AggName> "(" <distinct:"DISTINCT"?> <arg:AggArg> ")" => {
        parser_state.expr_graph.add_node(AggFunction(aggtype, distinct.is_some()), Some(vec![arg]))

    }
}

AggName: AggType = {
    "MIN" => AggType::MIN,
    "MAX" => AggType::MAX, 
    "SUM" => AggType::SUM, 
    "AVG" => AggType::AVG,
    "COUNT" => AggType::COUNT,
}

AggArg: ExprKey = {
    <arg:Expr> => arg,
    "*" => parser_state.expr_graph.add_node(Star, None)
}

ScalarFunction: ExprKey = {
    <name:Identifier> "(" <args:FunctionArgs?> ")" => parser_state.expr_graph.add_node(ScalarFunction(name), args),
    "CAST" "(" <arg:Expr> "AS" <datatype:Identifier> ")" => {
        let datatype = parser_state.expr_graph.add_node(Literal(Datum::STR(Rc::new(datatype.to_string()))), None);
        let args = vec![arg, datatype];
        parser_state.expr_graph.add_node(ScalarFunction(String::from("CAST")), Some(args))
    }
}

FunctionArgs = CommaSeparatedList<Expr>;

Number: ExprKey = {
    Integer =>? { isize::from_str(<>)
        .map(|i| parser_state.expr_graph.add_node(Literal(Datum::INT(i)), None))
        .map_err(|_| ParseError::User { error: "Invalid integer" }) 
    },
}

Integer = r"[-+]?[0-9]+";

Double = r"[-+]?\d*\.\d*";

Identifier: String = {
    r"[a-zA-Z][a-zA-Z0-9_-]*" => <>.to_string().to_uppercase()
}

ColumnReference: (Option<String>, String) = <tablename:(Identifier ".")?> <colname:Identifier> => (tablename.map(|e| e.0), colname);

QuotedString: String = {
    r#""[^"]*""# => {
        let s = <>;
        s[1..s.len()-1].to_string()
    },
    r#"'[^']*'"# => {
        let s = <>;
        s[1..s.len()-1].to_string()
    }
}

IdentifierOrQuotedString: String = {
    Identifier,
    QuotedString
}
